!!********************************************************************
!!Manual Thin Sequence building
!!********************************************************************

mylhcbeam = cp_mylhcbeam;

call,file="slhc/toolkit/macro.madx";        !Call some macro facilities

Option, -echo,-warn,-info;
if (mylhcbeam==4){
  call,file="lhc/lhcb4.seq";
} else {
  call,file="lhc/lhc.seq";
};
Option, -echo,warn,-info;
!
!!Install HL-LHC
call,file="slhc/hllhc_sequence.madx";

! Slice nominal sequence
exec,myslice;

call,file="slhc/errors/install_mqxf_fringenl.madx";    ! adding fringe place holder
call,file="slhc/errors/install_MCBXFAB_errors.madx";   ! adding D1 corrector placeholders in IR1/5 (for errors)
call,file="slhc/errors/install_MCBRD_errors.madx";     ! adding D2 corrector placeholders in IR1/5 (for errors)

!Cycling w.r.t. to IP3 (mandatory to find closed orbit in collision in the presence of errors)
if (mylhcbeam<3){
  seqedit,sequence=lhcb1;flatten;cycle,start=IP3;flatten;endedit;
};
seqedit,sequence=lhcb2;flatten;cycle,start=IP3;flatten;endedit;

!********************************************
!NRJ= 450.0  ; ! injection
NRJ= 7000.0 ; ! collision

! A Laundau octupole current 20A inj, -570A col
I_MO=cp_oct_current;


emittance_norm := cp_emit_beam*1.0E-6;
Nb_0:=cp_beam_npart; 

! Optics
call, file="myoptics.madx";

! Beam

gamma_rel := cp_beam_energy_tot / pmass;
epsx := cp_beam_norm_emit / gamma_rel;
epsy := cp_beam_norm_emit / gamma_rel;

bv_aux = -1;
if (mylhcbeam<3){
  beam,particle=proton,sequence=lhcb1,energy=cp_beam_energy_tot,
      sigt=cp_sigt,bv=1,
      npart=cp_beam_npart,sige=cp_beam_sige,ex=epsx,ey=epsy;
  bv_aux = 1;
};
beam,particle=proton,sequence=lhcb2,energy=cp_beam_energy_tot,
    sigt=cp_sigt,bv=-bv_aux,
    npart=cp_beam_npart,sige=cp_beam_sige,ex=epsx,ey=epsy;

! Tunes

tsplit= cp_qx00;
qx00  = cp_qy00;
qy00  = cp_tsplit;
qx0 = cp_qx0;
qy0 = cp_qy0;

qprime = cp_chromaticity;


!********************************************



if (NRJ<4999.9999){
  !Inj. optics in thin lens
  call,file="slhc/ramp/opt_inj_6000_thin.madx";  ! beta* [m]=6/10/6/10 in IR1/2/5/8
  qx0 = 62.27;   qy0 = 60.295;    if (mylhcbeam<3){
    Beam,particle=proton,sequence=lhcb1,energy=NRJ,sigt=0.130,bv=1,NPART=Nb_0,sige=4.5e-4,ex=epsx,ey=epsy;
  };
  Beam,particle=proton,sequence=lhcb2,energy=NRJ,sigt=0.130,bv=-bv_aux,NPART=Nb_0,sige=4.5e-4,ex=epsx,ey=epsy;
};

if (NRJ>5000.0000){
  if (choose_flat_optics==1) {
    call,file="slhc/flat/opt_flatvh_75_300_1500_thin.madx";
  } else {
    call,file="slhc/round/opt_round_150_1500_thin.madx";
  };
  qx0 = 62.31;   qy0 = 60.32;  qprime = cp_chromaticity;
  ON_QPP     :=0;  ! Correction of residual Q'' by MO's
  ON_DISP    :=1;  ! Correction of spurious dispersion
  if (mylhcbeam<3){
    Beam,particle=proton,sequence=lhcb1,energy=NRJ,sigt=sigt_col,bv=1,
        NPART=Nb_0,sige=1.1e-4,ex=epsx,ey=epsy;
  };
  Beam,particle=proton,sequence=lhcb2,energy=NRJ,sigt=sigt_col,bv=-bv_aux,
      NPART=Nb_0,sige=1.1e-4,ex=epsx,ey=epsy;
};

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!   Rematch IP1 IP5 phase
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
! nominal round: mux_ip15 = 31.21038468 ; muy_ip15 = 30.37288037 ;
! nominal injection: mux_ip15 = 31.19538482 ; muy_ip15 = 30.36788031 ;

!mux_ip15 = 31.21038468;
!muy_ip15 = 31.19538482;

!call,file="slhc/toolkit/make_ip15phase.madx";
!call,file=slhc/toolkit/make_ip15phase_tm.madx; ! use phase-trombone instead
!call,file=slhc/toolkit/delete_ip15phase_tm.madx; ! remove phase trombone

! test new optics
if (mylhcbeam==1){
  exec,check_ip(b1);
} else {
  exec,check_ip(b2);
};
mux_ip15_ref=table(twiss,IP1,mux)-table(twiss,IP5,mux);
muy_ip15_ref=table(twiss,IP1,muy)-table(twiss,IP5,muy);
value,mux_ip15_ref,muy_ip15_ref;


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!Set crossing angle and separations knobs
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;

!phi_IR1 = 0.000;    phi_IR5 =   90.000;    ! these are automatically set by the optics

if (NRJ<4999.9999){
  if (not_a_mask==1){ xing15=295; } else { xing15=cp_xing_ang_ip15; };
  on_x1= xing15;      on_sep1=-2;  
  on_x5= xing15;      on_sep5= 2;  
  on_x2= 170;         on_sep2= 3.5;
  on_x8=-170;         on_sep8=-3.5;
  on_a1=  0;          on_o1= 0;  
  on_a5=  0;          on_o5= 0;
  on_a2=-40;          on_o2= 0;          
  on_a8=-40;          on_o8= 0;
  on_crab1=0;
  on_crab5=0;
  on_disp=0;
};
if (NRJ>5000.0000){
  if (not_a_mask==1){ xing15=250; } else { xing15=cp_xing_ang_ip15; };
  on_x1= xing15;      on_sep1=-0.75;  
  on_x5= xing15;      on_sep5= 0.75;  
  on_x2= 170;         on_sep2= 1;
  on_x8=-200;         on_sep8=-1;
  on_a1= 0;           on_o1= 0;           
  on_a5= 0;           on_o5= 0;
  on_a2= 0;           on_o2= 0;
  on_a8= 0;           on_o8= 0;
  on_crab1=-190;
  on_crab5=-190;           
  on_disp=1;
};

! spectrometers in experiments
on_alice=7000/nrj;
on_lhcb =7000/nrj;

!Avoid crabbing more than the crossing angle
if ( abs(on_crab1)>abs(xing15) && on_crab1 <> 0) {on_crab1 = abs(on_crab1)/on_crab1 * abs(xing15);}
if ( abs(on_crab5)>abs(xing15) && on_crab5 <> 0) {on_crab5 = abs(on_crab5)/on_crab5 * abs(xing15);}

! solenoids in experiments
on_sol_atlas=0;
on_sol_cms=  0;
on_sol_alice=0; 

exec, print_crossing_knobs;
exec, crossing_save;

!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!                        TEST the optics
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++;
!/*

exec, crossing_disable;

system,"rm -f twiss.b1";system,"rm -f twiss.b2";

if (mylhcbeam<3){
  Use, sequence=lhcb1;
  select,flag=twiss,clear;
  select, flag=twiss, pattern="IP1",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  select, flag=twiss, pattern="IP2",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  select, flag=twiss, pattern="IP5",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  select, flag=twiss, pattern="IP8",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
  twiss,sequence=lhcb1,file=twiss.b1;system,"cat twiss.b1";
};

Use, sequence=lhcb2;
select,flag=twiss,clear;
select, flag=twiss, pattern="IP1",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
select, flag=twiss, pattern="IP2",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
select, flag=twiss, pattern="IP5",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
select, flag=twiss, pattern="IP8",   column=name,s,betx,bety,alfx,alfy,dx,dpx,mux,muy;
twiss, sequence=lhcb2,file=twiss.b2;system,"cat twiss.b2";

system,"rm -f twiss.b1";system,"rm -f twiss.b2";
!Switch on Xscheme
exec,crossing_restore;

if (mylhcbeam<3){
  Use, sequence=lhcb1;
  select,flag=twiss,clear;
  select, flag=twiss, pattern="IP1",   column=name,s,x,y,px,py;
  select, flag=twiss, pattern="IP2",   column=name,s,x,y,px,py;
  select, flag=twiss, pattern="IP5",   column=name,s,x,y,px,py;
  select, flag=twiss, pattern="IP8",   column=name,s,x,y,px,py;
  twiss, sequence=lhcb1, file=twiss.b1;system,"cat twiss.b1";
};

xnom1=table(twiss,IP1,x);pxnom1=table(twiss,IP1,px);ynom1=table(twiss,IP1,y);pynom1=table(twiss,IP1,py);
xnom2=table(twiss,IP2,x);pxnom2=table(twiss,IP2,px);ynom2=table(twiss,IP2,y);pynom2=table(twiss,IP2,py);
xnom5=table(twiss,IP5,x);pxnom5=table(twiss,IP5,px);ynom5=table(twiss,IP5,y);pynom5=table(twiss,IP5,py);
xnom8=table(twiss,IP8,x);pxnom8=table(twiss,IP8,px);ynom8=table(twiss,IP8,y);pynom8=table(twiss,IP8,py);

Use, sequence=lhcb2;
select,flag=twiss,clear;
select, flag=twiss, pattern="IP1",   column=name,s,x,y,px,py;
select, flag=twiss, pattern="IP2",   column=name,s,x,y,px,py;
select, flag=twiss, pattern="IP5",   column=name,s,x,y,px,py;
select, flag=twiss, pattern="IP8",   column=name,s,x,y,px,py;
twiss, sequence=lhcb2, file=twiss.b2;system,"cat twiss.b2";

value,xnom1,xnom2,xnom5,xnom8;
value,ynom1,ynom2,ynom5,ynom8;
value,pxnom1,pxnom2,pxnom5,pxnom8;
value,pynom1,pynom2,pynom5,pynom8;
